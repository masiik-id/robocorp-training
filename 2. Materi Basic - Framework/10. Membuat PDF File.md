# Membuat file PDF
Maria membuat PDF dari tabel data penjualan di intranet dan mengirimkannya sebagai buletin perusahaan untuk memastikan rekan-rekannya melihatnya. 

Sudah gitu dia melakukannya dengan copy-paste, lebih sedih lagi kalo teman tim nggak ada yang lihat. perjuangan dia melakukan copy paste dari website ke ms. word lalu mengubah ke PDF. 

dengan Robot kita akan mempermudah pekerjaannya untuk melakukan Rekapitulasi
![images](https://robocorp.com/docs/static/get-started/courses/shared-assets/beginners-course/html-table-to-pdf-file.png)

tambahkan logika baru pada *** Tasks *** seperti di bawah ini 
````bash
*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results
    Export the table as a PDF
````

dan kita menambahkah keyword baru 
````bash
*** Keywords ***
Export the table as a PDF
```````

Rencana kita pada keyword ini adalah :
- kita mengisolasi bagian halaman yang berisi tabel penjualan
- kita menetapkan konten (markup HTML) dari bagian halaman tersebut ke variabel
- kami membuat PDF dengan konten HTML tabel.

## Mengeluarkan elemen tabel HTML dari halaman
kita akan memastikan bahwa element TABLE itu ada saat kita akan mengambilnya. kita akan menggunakan keyword  ````Wait Until Element Is Visible ```` 

Markup HTML area tabel pada halaman terlihat seperti ini:
````html
...
<div id="sales-results">
  <table class="table table-dark table-striped">
    ...
  </table>
</div>
...
````

Tabel dibungkus dalam elemen <div> dengan atribut id dari sales-results. Pencari lokasi kami kemudian akan menjadi id:sales-results

kita akan memodifikasi keywornya menjadi seperti ini 
```bash
*** Keywords ***
Export the table as a PDF
    Wait Until Element Is Visible    id:sales-results
````

Selanjutnya, kita ingin memasukkan markup HTML elemen tersebut ke dalam variabel. Kita bisa melakukan ini dengan Get Element Attribute keyword (RPA.Browser.Selenium library) kaya begini:

````
*** Keywords ***
Export the table as a PDF
    Wait Until Element Is Visible    id:sales-results
    ${sales_results_html}=    Get Element Attribute    id:sales-results    outerHTML
````

Oke, kami akui ini tidak terlalu mudah ditebak. ðŸ˜… Tapi jangan panik! Mari kita lihat apa yang terjadi pada baris baru ini.
 
kita membuat variabel (${sales_results_html}=). Kami menyimpan ke dalamnya apa yang kami dapatkan dari keyword Get Element Attribute. Kami meneruskan dua argumen ke kata kunci itu: yang pertama adalah pencari lokasi elemen (id:sales-results). yang kedua adalah nama atribut dari elemen yang ingin kita dapatkan.Dalam kasus kami, kami ingin semua markup HTML elemen itu, termasuk semua yang ada di dalamnya, jadi kami memilih atribut outerHTML.

log yang akan di tampilkan ole robot adalah seperti ini
![images](https://robocorp.com/docs/static/get-started/courses/beginners-course/creating-pdf/log-html-table.png)


## Membuat file PDF dari variabel konten HTML
Sekarang kita memiliki konten HTML tabel dalam sebuah variabel, kita perlu membuat file PDF darinya. Untuk melakukannya, kami akan menambahkan perpustakaan RPA.PDF!
````bash
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.Excel.Files
Library           RPA.HTTP
Library           RPA.PDF
````

Sekarang kita dapat menambahkan baris terakhir ke kata kunci kita.

````bash
*** Keywords ***
Export the table as a PDF
    Wait Until Element Is Visible    id:sales-results
    ${sales_results_html}=    Get Element Attribute    id:sales-results    outerHTML
    Html To Pdf    ${sales_results_html}    ${OUTPUT_DIR}${/}sales_results.pdf
````

Kita menggunakan kata kunci ````Html To Pdf```` yang disediakan oleh library ````RPA.PDF```` untuk membuat file ````sales_results.pdf```` dari konten variabel ````${sales_results_html}````, dan menempatkannya lagi ke folder output ````(${OUTPUT_DIR}${/})````.

inilah akhir dari robot kita
````bash
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.Excel.Files
Library           RPA.HTTP
Library           RPA.PDF

*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results
    Export the table as a PDF

*** Keywords ***
Open the intranet website
    Open Available Browser    https://robotsparebinindustries.com/

Log in
    Input Text    username    maria
    Input Password    password    thoushallnotpass
    Submit Form
    Wait Until Page Contains Element    id:sales-form

Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True

Fill and submit the form for one person
    [Arguments]    ${sales_rep}
    Input Text    firstname    ${sales_rep}[First Name]
    Input Text    lastname    ${sales_rep}[Last Name]
    Input Text    salesresult    ${sales_rep}[Sales]
    Select From List By Value    salestarget    ${sales_rep}[Sales Target]
    Click Button    Submit

Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
    FOR    ${sales_rep}    IN    @{sales_reps}
        Fill and submit the form for one person    ${sales_rep}
    END

Collect the results
    Screenshot    css:div.sales-summary    ${OUTPUT_DIR}${/}sales_summary.png

Export the table as a PDF
    Wait Until Element Is Visible    id:sales-results
    ${sales_results_html}=    Get Element Attribute    id:sales-results    outerHTML
    Html To Pdf    ${sales_results_html}    ${OUTPUT_DIR}${/}sales_results.pdf
````


## Apa yang kita pelajari?
- kita dapat meggunakan keyword  Get Element Attribute  dari library  RPA.Browser.Selenium untuk mendapatkan markup HTML sebenarnya dari elemen apa pun menggunakan atribut outerHTML. 
- Anda dapat membuat file PDF dari konten HTML menggunakan perpustakaan RPA.PDF dan kata kunci Html To Pdf