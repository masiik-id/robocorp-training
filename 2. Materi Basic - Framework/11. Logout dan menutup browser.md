# Logout dan menutup browser
Setelah kita melakukan action sebelumnya, jika sudah selesai maka kita harus menutup browser yang kita gunakan. 

mari kita buat task terakhir dengan nama Log out and close the browser
````bash
*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results
    Export the table as a PDF
    Log out and close the browser
````
tambahin keywordnya dong
````
*** Keywords ***
Log out and close the browser
````
Jadi, dua hal. Keluar. Tutup peramban. Anda sudah mengetahui tentang kata kunci Tombol Klik, jadi mari kita gunakan kata kunci tersebut untuk logout

````
*** Keywords ***
Log out and close the browser
    Click Button    Log out
````

Bagaimana cara kita menutup browser? Ada kata kunci untuk itu juga (sekali lagi terima kasih, RPA.Browser.Selenium!)!:

````
*** Keywords ***
Log out and close the browser
    Click Button    Log out
    Close Browser
````

# Tearing things down
Kami ingin memastikan bahwa logout dan penutupan browser terjadi meskipun beberapa bagian dari proses kami gagal dan robot gagal mencapai akhir tugas Untuk melakukannya, kami menggunakan kata kunci khusus: [Teardown]

modif sedikit tasknya 
````
*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results
    Export the table as a PDF
    [Teardown]    Log out and close the browser
````

Bagaimana jika kita memutuskan untuk tidak menyertakan [Teardown]? Dalam hal itu,logout dan penutupan browser tidak akan terjadi jika salah satu kata kunci kita mengalami kegagalan pada saat eksekusi. Dengan menggunakan kata kunci [Teardown] dan memberikan kata kunci kami sebagai argumen, kami memastikan bahwa apa pun yang terjadi, setidaknya langkah pembersihan selalu terjadi

### Done 
````
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.Excel.Files
Library           RPA.HTTP
Library           RPA.PDF

*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results
    Export the table as a PDF
    [Teardown]    Log out and close the browser

*** Keywords ***
Open the intranet website
    Open Available Browser    https://robotsparebinindustries.com/

Log in
    Input Text    username    maria
    Input Password    password    thoushallnotpass
    Submit Form
    Wait Until Page Contains Element    id:sales-form

Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True

Fill and submit the form for one person
    [Arguments]    ${sales_rep}
    Input Text    firstname    ${sales_rep}[First Name]
    Input Text    lastname    ${sales_rep}[Last Name]
    Input Text    salesresult    ${sales_rep}[Sales]
    Select From List By Value    salestarget    ${sales_rep}[Sales Target]
    Click Button    Submit

Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
    FOR    ${sales_rep}    IN    @{sales_reps}
        Fill and submit the form for one person    ${sales_rep}
    END

Collect the results
    Screenshot    css:div.sales-summary    ${OUTPUT_DIR}${/}sales_summary.png

Export the table as a PDF
    Wait Until Element Is Visible    id:sales-results
    ${sales_results_html}=    Get Element Attribute    id:sales-results    outerHTML
    Html To Pdf    ${sales_results_html}    ${OUTPUT_DIR}${/}sales_results.pdf

Log out and close the browser
    Click Button    Log out
    Close Browser
````


## Apa yang kita pelajari
- Kata kunci [Teardown] memastikan proses Anda tidak meninggalkan kekacauan. Seperti browser yang terbuka.