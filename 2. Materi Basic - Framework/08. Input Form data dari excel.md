# Menginput Form dengan data dari excel
Robot kita akan mempelajari beberapa keterampilan serius pada langkah ini. Bab ini mungkin sedikit lebih rumit daripada apa yang telah kita lihat sejauh ini. Seperti biasa, kita akan mengatasinya selangkah demi selangkah, jadi jangan khawatir!

Mari kita rekap dimana kita berada. Sejauh ini, robot kita mampu:
- Masuk kedalam Intranet
- Mengisi form menggunakan Data Hard code
- Download data dari internet dan menyimpan ke local

Sekarang kita perlu membaca data penjualan dari file Excel lalu mengisi form untuk setiap baris di dalamnya.

## Membaca Excel file
Pada titik ini, dalam tugas mingguannya yang menyakitkan, Maria akan menjalankan Microsoft Excel di komputernya dan mulai menyalin-menempelkan aplikasi ke formulir sampai matanya tidak dapat menangkapnya, mencoba memikirkan hal-hal yang membahagiakan... ðŸ’©

Robot kita tidak akan melakukan copy-paste; bahkan tidak membutuhkan aplikasi excel sama sekali. Kita bisa mengajarkannya untuk membuka dan membaca file Excel secara langsung. Bagaimana? Dengan menambahkan Library baru tentunya!

Mari tambahkan library RPA.Excel.Files ke bagian *** Settings ***:
````bash
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.HTTP
Library           RPA.Excel.Files
````

mari kita ubah logika <i>Fill and submit the form</i> di bagian  *** Tasks *** menjadi <i>Fill the form using the data from the Excel file</i> untuk menjelaskan dengan lebih baik apa yang akan dilakukan robot: 

````bash
*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
````

Tambahkan keyword diatas, dengan Library RPA.Excel.Files robot kita dapat membuka file excel.
````bash
*** Keywords ***
Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
````
Mari kita lihat kembali file Excelnya:
![images](https://robocorp.com/docs/static/get-started/courses/shared-assets/beginners-course/beginner-course-excel.png)

Baris pertama berisi header tabel (First Name, Last Name, Sales dan Sales Target). Lalu kita memiliki baris untuk masing-masing sales representatives. Robot kita perlu membaca data ini dalam format yang dapat dimengerti. Kita bisa melakukannya dengan kata kunci Read Worksheet As Table

Ini mungkin terdengar sulit, tetapi Anda bisa melakukannya! Mari kita lakukan selangkah demi selangkah.

Kita akan memasukkan data tersebut ke dalam variabel yang kita sebut ${sales_reps}. Anggap saja variabel sebagai wadah untuk sesuatu. Seperti sales representatives! Selain itu, karena Excel kita berisi header pada baris pertama, kita akan meneruskan parameter header sebagai True

Setelah ini, kita tidak perlu lagi membiarkan file Excel tetap terbuka. Kita bisa menggunakan kata kunci Close Workbook untuk menutupnya. 

skriptnya akan terlihat seperti ini

````bash
*** Keywords ***
Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
````

kita belum melakukan apapun ya pada sckrip diatas. 
skrip kita saat ini : 

````bash
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.HTTP
Library           RPA.Excel.Files

*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file

*** Keywords ***
Open the intranet website
    Open Available Browser    https://robotsparebinindustries.com/

Log in
    Input Text    username    maria
    Input Password    password    thoushallnotpass
    Submit Form
    Wait Until Page Contains Element    id:sales-form

Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True

Fill and submit the form
    Input Text    firstname    John
    Input Text    lastname    Smith
    Input Text    salesresult    123
    Select From List By Value    salestarget    10000
    Click Button    Submit

Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
````

yuk jalankan dan kita check Log nya 
![images](https://robocorp.com/docs/static/get-started/courses/beginners-course/filling-in-the-sales-form-using-excel-data/beginner-course-excel-log.png)

## Menisi Form dengan data seperti di excel

Ingat keyword kita <b>Fill and submit the form</b>? yang kita buat beberapa saat lalu dengan data hardcode

````bash
*** Keywords ***
Fill and submit the form
    Input Text    firstname    John
    Input Text    lastname    Smith
    Input Text    salesresult    123
    Select From List By Value    salestarget    10000
    Click Button    Submit
````

kita akan menggunakan argumen ${sales_rep} berupa list dari nama. Jika, misalnya, kita ingin mengakses properti variabel First Name, kita akan menulis: ${sales_rep}[First Name]. First Name dari mana ???

Kita bisa mengucapkan selamat tinggal kepada teman kita John Smith, dan mengganti nilai-nilai hardcode kita seperti ini:
````bash
*** Keywords ***
Fill and submit the form for one person
    [Arguments]    ${sales_rep}
    Input Text    firstname    ${sales_rep}[First Name]
    Input Text    lastname    ${sales_rep}[Last Name]
    Input Text    salesresult    ${sales_rep}[Sales]
    Select From List By Value    salestarget    ${sales_rep}[Sales Target]
    Click Button    Submit
````

## Mengisi formulir untuk setiap baris
tadi kita sudah membuat untuk memasukan Form ke setiap orang, saatnya kembali ke keyword sebelumnya : Fill the form using the data from the Excel file

````bash
*** Keywords ***
Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
````

Fill and submit the form for one person sudah siap menerima argumen, Kita bisa mengulang baris-baris tabel  ( ${sales_reps} variable) dan panggil setiap kali meneruskan baris individual ke sana.

````bash
*** Keywords ***
Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
    FOR    ${sales_rep}    IN    @{sales_reps}
        Fill and submit the form for one person    ${sales_rep}
    END
````

sehingga robot kita saat ini tampak seperti ini
````bash
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.HTTP
Library           RPA.Excel.Files

*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file

*** Keywords ***
Open the intranet website
    Open Available Browser    https://robotsparebinindustries.com/

Log in
    Input Text    username    maria
    Input Password    password    thoushallnotpass
    Submit Form
    Wait Until Page Contains Element    id:sales-form

Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True

Fill and submit the form for one person
    [Arguments]    ${sales_rep}
    Input Text    firstname    ${sales_rep}[First Name]
    Input Text    lastname    ${sales_rep}[Last Name]
    Input Text    salesresult    ${sales_rep}[Sales]
    Select From List By Value    salestarget    ${sales_rep}[Sales Target]
    Click Button    Submit

Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
    FOR    ${sales_rep}    IN    @{sales_reps}
        Fill and submit the form for one person    ${sales_rep}
    END
````


## Apa yang kita pelajari
- anda dapat menggunakan library RPA.Excel.Files, tanpa menggunakan / membuka excel
- Anda dapat memberikan argumen pada keyword Anda.
- Anda dapat membuka dan menutup file Excel dengan keyword Open Workbook dan Close Workbook
- Anda dapat mengulang variabel dengan sintaks FOR...IN
