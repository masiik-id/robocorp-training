# Mengumpulkan hasil
Semua data penjualan telah dimasukkan. Bagus! Setelah memasukkan semua data penjualan, Maria mengambil screenshot ringkasan penjualan:

![images](https://robocorp.com/docs/static/get-started/courses/shared-assets/beginners-course/sales-summary-in-page.png)

Robot kami juga dapat menangani kasus ini!
Mari tambahkan logika pada tasks kita. kita  panggil sebut saja Collect the results

````bash
*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results
````

dan sebuah keyword baru
````bash
*** Keywords ***
Collect the results
````

## Mengambil screenshot
 RPA.Browser.Selenium menyediakan keyword/library Screenshoot. untuk membantu kita dalam langkah ini. kita  memberinya locator element yang ingin kami ambil screenshoot dan path untuk file gambar.

 ## Menemukan elemen
 Mari kita lihat apakah halaman markup HTML  tersebut berisi locator yang dapat kita gunakan:

 ````html
 ...
<div class="alert alert-dark sales-summary" role="alert">
  <div><span>Active sales people:</span>...</div>
</div>
...
 ````

 [Klik ini](https://robocorp.com/docs/development-guide/browser/how-to-find-user-interface-elements-using-locators-in-web-applications) untuk mempelajari cara mencari Locator

 Kita punya Elemen div, dia punya attribut class dan role.
 Karena tidak ada atribut id atau nama untuk digunakan, kita memutuskan untuk menggunakan CSS classes  untuk menargetkannya. sales-summary sepertinya merupakan kandidat yang baik karena dengan demikian akan jelas dalam skrip kita apa yang kita ambil tangkapan layarnya.
 Karena kita menggunakan CSS untuk mencari locator element,  locator kita akan dimulai dengan awalan css:. Ada beberapa jenis locator: id, nama, xpath, bergantung pada strategi yang digunakan.

 Locator css:div.sales-summary berarti yang kita pakai:
 <br/>Dengan menggunakan strategi CSS, carikan saya elemen div yang memiliki kelas CSS sales-summary

 ## Menyimpan file ke direktori Output
 seperti argumen kedua, kita akan menggunkan Screenshoot kemudian menambahkannya ke folder output.
 PATH untuk file tersebut adalah ini  ```` ${OUTPUT_DIR}${/}sales_summary.png````

 Tapi apa saja variabel tersebut?
<br/>
```` ${OUTPUT_DIR} ````  adalah [Robot Framework runtime variable](https://robocorp.com/docs/languages-and-frameworks/robot-framework/variables#runtime-variables) yang mewakili the output directory.
<br>
```` ${/} ```` adalah variabel bawaan yang mewakili pemisah directory path (/ atau \). Variabel ini memastikan pemisah jalur akan berfungsi di sistem operasi apa pun (macOS, Windows, Linux).

maka Implementasi keywordnya adalah seperti ini.
````bash
*** Keywords ***
Collect the results
    Screenshot    css:div.sales-summary    ${OUTPUT_DIR}${/}sales_summary.png
````
dan Secara Robot kita akan menjadi seperti ini :
````
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.Excel.Files
Library           RPA.HTTP

*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill the form using the data from the Excel file
    Collect the results

*** Keywords ***
Open the intranet website
    Open Available Browser    https://robotsparebinindustries.com/

Log in
    Input Text    username    maria
    Input Password    password    thoushallnotpass
    Submit Form
    Wait Until Page Contains Element    id:sales-form

Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True

Fill and submit the form for one person
    [Arguments]    ${sales_rep}
    Input Text    firstname    ${sales_rep}[First Name]
    Input Text    lastname    ${sales_rep}[Last Name]
    Input Text    salesresult    ${sales_rep}[Sales]
    Select From List By Value    salestarget    ${sales_rep}[Sales Target]
    Click Button    Submit

Fill the form using the data from the Excel file
    Open Workbook    SalesData.xlsx
    ${sales_reps}=    Read Worksheet As Table    header=True
    Close Workbook
    FOR    ${sales_rep}    IN    @{sales_reps}
        Fill and submit the form for one person    ${sales_rep}
    END

Collect the results
    Screenshot    css:div.sales-summary    ${OUTPUT_DIR}${/}sales_summary.png
````

Saat kita menjalankan robotnya, screenshoot akan disimpan di dalam folder output.


## Apa yang kita pelajari 
- keyword screenshoot dari library Selenium untuk mengambil tangkapan layar
- Mengetahui cara menggunakan pencari lokasi akan membantu Anda dalam mengotomatisasi aplikasi web.
- Ada banyak jenis pencari lokasi.
- Variabel runtime ${OUTPUT_DIR} mewakili direktori output robot.
- Variabel bawaan ${/} mewakili pemisah jalur untuk sistem operasi saat ini
- Folder output adalah tempat kita meletakkan file yang dihasilkan robot kita.