# Downloading the Excel file
Setiap minggu, data penjualan aktual diekspor dari sistem penjualan perusahaan dan diunggah ke intranet. Robot kami perlu mengambil data dari file Excel dan mengisi formulir penjualan untuk setiap  sales representative. Tapi pertama-tama, perlu mendownload file Excelnya.

Tambahkan logika "Download the excel file" pada task
````
*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill and submit the form
````

Kemudian tambahkan keyword baru 
````bash
*** Keywords ***
Download the Excel file
````

# Tambahkan RPA.HTTP library
Sekarang kita bisa menggunakan semua kata kunci yang terdapat di perpustakaan RPA.HTTP, termasuk kata kunci Download.

Kata kunci Download memerlukan URL sebagai argumen. kita dapat mendapatkan file Excel  di https://robotsparebinindustries.com/SalesData.xlsx setiap minggunya. 

Selain argumen URL, selain itu kita juga menyeting argumen overwrite ke True. Dengan cara ini, kami dapat mengandalkan file lokal untuk selalu menjadi versi terbaru (jika file tersebut ada, robot memiliki izin kami untuk menimpanya).

````bash
*** Keywords ***
Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True
````

Dengan tambahan ini, robot kita terlihat seperti ini:
````bash
*** Settings ***
Documentation     Insert the sales data for the week and export it as a PDF.
Library           RPA.Browser.Selenium    auto_close=${FALSE}
Library           RPA.HTTP

*** Tasks ***
Insert the sales data for the week and export it as a PDF
    Open the intranet website
    Log in
    Download the Excel file
    Fill and submit the form

*** Keywords ***
Open the intranet website
    Open Available Browser    https://robotsparebinindustries.com/

Log in
    Input Text    username    maria
    Input Password    password    thoushallnotpass
    Submit Form
    Wait Until Page Contains Element    id:sales-form

Download the Excel file
    Download    https://robotsparebinindustries.com/SalesData.xlsx    overwrite=True

Fill and submit the form
    Input Text    firstname    John
    Input Text    lastname    Smith
    Input Text    salesresult    123
    Select From List By Value    salestarget    10000
    Click Button    Submit
````


## Apa yag kita pelajari
- Anda dapat menggunakan Library RPA.HTTP, bagian dari kumpulan rpaframework automation libraries,, untuk berinteraksi dengan server web jarak jauh.
- Anda dapat menggunakan kata kunci Download untuk mengunduh file jarak jauh dengan mudah ke komputer lokal Anda
- Kata kunci Download dapat dikonfigurasi untuk menimpa file yang sudah ada dengan nama yang sama jika ditemukan
